set dotenv-required
set dotenv-load
set shell := ["bash", "-uc"]
set windows-shell := ["bash", "-uc"]

_default:
    @just --list --justfile {{ '{{ justfile() }}' }}

# Generate everything
[group("generators")]
gen-all: clean gen-json-schema gen-docs

# Generate JSON Schema
[group("generators")]
gen-json-schema:
    @echo "Generating JSON Schema…"
    @echo -en "\t"
    mkdir -p "$DP_ARTIFACTS_DIR/schemas/json_schema"
    @echo -en "\t"
    poetry run gen-json-schema \
        --not-closed \
        "$DP_LINKML_SCHEMA" \
        > "$DP_ARTIFACTS_DIR/schemas/json_schema/$DP_FILENAME.json_schema.json"
    @echo -n "… "
    @echo "OK."
    @echo
    @echo -e "Generated JSON Schema at: $DP_ARTIFACTS_DIR/schemas/json_schema/$DP_FILENAME.json_schema.json"
    @echo

# Post-process LinkML schema for preview or releasing
_post-process-linkml-schema:
    mkdir -p "$DP_ARTIFACTS_DIR/information-models"
    cp "$DP_LINKML_SCHEMA" "$DP_ARTIFACTS_DIR/information-models/${DP_FILENAME}.schema.linkml.yaml"
    sed -i "/^name: .*$/a version: $(git rev-parse --abbrev-ref HEAD)" "$DP_ARTIFACTS_DIR/information-models/${DP_FILENAME}.schema.linkml.yaml"

# Generate schema documentation (as an Antora module)
[group("generators")]
gen-docs: _post-process-linkml-schema
    @echo "Generating documentation…"
    @echo -en "\t"
    cp -r "$DP_DOCS_DIR" "$DP_ARTIFACTS_DIR"
    mkdir -p "$DP_ARTIFACTS_DIR/documentation/modules/information-model"
    @echo -en "\t"
    poetry run python -m linkml_asciidoc_generator.main \
        "$DP_LINKML_SCHEMA" \
        "$DP_ARTIFACTS_DIR/documentation/modules/information-model"
    sed -i "s/version: true/version: $(git rev-parse --abbrev-ref HEAD)/" "$DP_ARTIFACTS_DIR/documentation/antora.yml"
    cp "$DP_ARTIFACTS_DIR/information_models/${DP_FILENAME}.schema.linkml.yaml" "$DP_ARTIFACTS_DIR/documentation/modules/ROOT/attachments/
    cp "$DP_ARTIFACTS_DIR/schemas/*/*" "$DP_ARTIFACTS_DIR/documentation/modules/ROOT/attachments/
    @echo -n "… "
    @echo "OK."
    @echo
    @echo -e "Generated documentation files at: $DP_ARTIFACTS_DIR/documentation"
    @echo

# Clean up the output directory
[group("general"), confirm]
clean:
    @echo "Cleaning up generated artifacts…"
    @echo -e "\tCleaning up: $DP_ARTIFACTS_DIR"
    @if [ -d "$DP_ARTIFACTS_DIR" ]; then \
        rm -rf "$DP_ARTIFACTS_DIR" \
    else \
        mkdir -p "$DP_ARTIFACTS_DIR"; \
    fi
    @echo "… OK."
    @echo

# Edit the information model
[group("schema")]
edit-schema:
    @${VISUAL:-${EDITOR:-nano}} "$DP_LINKML_SCHEMA"

# Show class hierarchy in information model
[group("schema")]
show-schema-classes:
    yq '.classes.* | key' "$DP_LINKML_SCHEMA"

# Show class hierarchy in information model
[group("schema")]
get-def curie:
    @yq '.classes.* | select(.class_uri == "{{curie}}")' "$DP_LINKML_SCHEMA"

# Release new major version
[group("vcs")]
release-major-version:
    @echo "Releasing new major version…"
    @echo -en "\t"
    gh workflow run release_major_version.yaml --ref $(git rev-parse --abbrev-ref HEAD)
    @echo "… OK."
    @echo

# Release new minor version
[group("vcs")]
release-minor-version:
    @echo "Releasing new minor version…"
    @echo -en "\t"
    gh workflow run release_minor_version.yaml --ref $(git rev-parse --abbrev-ref HEAD)
    @echo "… OK."
    @echo

# Release new patch version
[group("vcs")]
release-patch-version:
    @echo "Releasing new patch version…"
    @echo -en "\t"
    gh workflow run release_patch_version.yaml --ref $(git rev-parse --abbrev-ref HEAD)
    @echo "… OK."
    @echo

# Preview version
[group("vcs")]
preview-version:
    @echo "Generating preview of version…"
    @echo -en "\t"
    gh workflow run preview_release.yaml --ref $(git rev-parse --abbrev-ref HEAD)
    @echo "… OK."
    @echo

# Add GitHub branch protection rules for documentation branches
[group("vcs")]
_protect-docs-branches:
    @echo "Adding ruleset to protect documentation branches…"
    @echo -en "\t"
    gh api \
        --method POST \
        -H "Accept: application/vnd.github+json" \
        -H "X-GitHub-Api-Version: 2022-11-28" \
        /repos/Netbeheer-Nederland/$(basename `git config --get remote.origin.url` | cut -d . -f -1)/rulesets \
        --input "$DP_SCRIPTS_DIR/protect-docs-branches-ruleset-def.json"
    @echo "… OK."
    @echo

# Add GitHub branch protection rules for major branches
[group("vcs")]
_protect-major-branches:
    @echo "Adding ruleset to protect major branches…"
    @echo -en "\t"
    gh api \
        --method POST \
        -H "Accept: application/vnd.github+json" \
        -H "X-GitHub-Api-Version: 2022-11-28" \
        /repos/Netbeheer-Nederland/$(basename `git config --get remote.origin.url` | cut -d . -f -1)/rulesets \
        --input "$DP_SCRIPTS_DIR/protect-major-branches-ruleset-def.json"
    @echo "… OK."
    @echo

# Add GitHub branch protection rules
[group("vcs")]
add-branch-protections: _protect-docs-branches _protect-major-branches
